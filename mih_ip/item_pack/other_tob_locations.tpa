DEFINE_ACTION_FUNCTION other_tob_locations
BEGIN
  // =============
  // Ancient Grove
  // =============

  // Illesara's Ambush
  //
  // mh#brac4 - Bracers of the Wind

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "illasera"
    editstring	= ~add_items=>"mh#brac4(gloves)"~
  END


  // ========
  // Saradush
  // ========

  // Arcana Archives
  //
  // mh#dart3 - Zilzanzer's Magnificent Darts +1
  // mh#potn1 - Oil of Acid Resistance
  // mh#potn4 - Oil of Disenchantment
  // mh#potn6 - Alchemist's Fire
  // mh#potn7 - Flask of Acid
  // mh#ring4 - Ring of White Bone
  // mh#ring9 - Ring of Thex
  // mh#tome3 - Libram of Silver Magic
  // mh#wand1 - Wand of Illusions

  MAKE_PATCH
    literal'01=>~ADD_STORE_ITEM "mh#dart3" AFTER "helm23" #12 #0 #0 "identified" #12~
    literal'02=>~ADD_STORE_ITEM "mh#tome3" AFTER "restore" #1 #0 #0 "identified" #1~
    literal'03=>~ADD_STORE_ITEM "mh#potn1" AFTER "potn02" #1 #0 #0 "identified" #9~
    literal'04=>~ADD_STORE_ITEM "mh#potn4" AFTER "potn55" #1 #0 #0 "identified" #5~
    literal'05=>~ADD_STORE_ITEM "mh#potn6" AFTER "potn45" #1 #0 #0 "identified" #10~
    literal'06=>~ADD_STORE_ITEM "mh#potn7" AFTER "mh#potn6" #1 #0 #0 "identified" #10~
    literal'07=>~ADD_STORE_ITEM "mh#ring9" AFTER "ring02" #0 #0 #0 "identified" #1~
    literal'08=>~ADD_STORE_ITEM "mh#ring4" AFTER "ring02" #12 #0 #0 "identified" #1~
    literal'09=>~ADD_STORE_ITEM "mh#wand1" LAST #12 #12 #0 "identified" #1~
  END

  LAUNCH_ACTION_FUNCTION edit_store
    STR_VAR
    store	= "25spell"
    edits	= "patch_data"
  END

  // Temple of Waukeen
  //
  // mh#bull1 - Priest's Bullet
  // mh#potn3 - Philter of Purification

  MAKE_PATCH
    literal'1=>~ADD_STORE_ITEM "mh#bull1" LAST #5 #0 #0 "identified" #10~
    literal'2=>~ADD_STORE_ITEM "mh#potn3" AFTER "potn20" #1 #0 #0 "identified" #6~
  END

  LAUNCH_ACTION_FUNCTION edit_store
    STR_VAR
    store	= "sartem01"
    edits	= "patch_data"
  END

  // Castle Saradush
  //
  // mh#brac6 - Silver Bracers of Kedl (on Karun the Black)
  // mh#staf4 - Staff of the Legion +5 (on Karun the Black)

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "gromg08"
    editstring	= ~remove_items=>"staf18" add_items=>"mh#brac6(gloves) mh#staf4(weapon1)"~
  END


  // ==================================
  // Forest of Mir / Marching Mountains
  // ==================================

  // Abandoned Temple of Bhaal
  //
  // mh#tome2 - Book of Vile Darkness
  // mh#ringb - Ring of the Hierophant (on Nyalee)
  // mh#robe5 - Nyalee's Robe of Stone (guess)

  MAKE_PATCH
    add_item_inline=>~match=>"parent_index = 1" item_resource=>"mh#tome2" item_charges_1=>1~
  END
  
  LAUNCH_ACTION_FUNCTION edit_area
    STR_VAR
    area	= "ar5202"
    edits	= "patch_data"
  END

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "hgnya01"
    editstring	= ~ac=>10 add_items=>"mh#robe5(armor,x1) mh#ringb(lring)" remove_items=>"brac15 ring03"~
  END

  // Fire Giant Temple
  //
  // mh#tome4 - Libram of Gainful Conjuration
  // hamm10   - Rune Hammer +4 (moved from Yaga-Shura)

  MAKE_PATCH
    add_item_inline=>~match=>"parent_index = 1" item_resource=>"mh#tome4" item_charges_1=>1~
  END
  
  LAUNCH_ACTION_FUNCTION edit_area
    STR_VAR
    area	= "ar5204"
    edits	= "patch_data"
  END

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "hgber01"
    editstring	= ~add_items=>"hamm10(inv)"~
  END


  // Yaga-Shura Battlefield
  //
  // mh#hamm2 - Maul of the Titans

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "yaga01"
    editstring	= ~add_items=>"mh#hamm1(weapon1)" remove_items=>"giafir hammr10"~
  END


  // =========
  // Amkethran
  // =========

  // Temple of Waukeen
  //
  // mh#bull1 - Priest's Bullet
  // mh#potn3 - Philter of Purification
  // mh#shld7 - Waukeen's Defender +2

  MAKE_PATCH
    literal'1=>~ADD_STORE_ITEM "mh#bull1" FIRST #5 #0 #0 "identified" #10~
    literal'2=>~ADD_STORE_ITEM "mh#potn3" AFTER "potn20" #1 #0 #0 "identified" #6~
    literal'3=>~ADD_STORE_ITEM "mh#shld7" LAST #0 #0 #0 "identified" #1~
  END

  LAUNCH_ACTION_FUNCTION edit_store
    STR_VAR
    store	= "amcler02"
    edits	= "patch_data"
  END

  // Amkethran Smugglers
  //
  // mh#shld8 - White Shield +4

  MAKE_PATCH
    literal'1=>~ADD_STORE_ITEM "mh#shld8" AFTER "plat21" #0 #0 #0 "identified" #1~
  END

  LAUNCH_ACTION_FUNCTION edit_store
    STR_VAR
    store	= "amsmug02"
    edits	= "patch_data"
  END

  // Sealed Cave
  //
  // mh#ringa - Ring of the Lich +3 (on Vongoethe, replaces Ring of Anti-Venom)

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "amlich01"
    editstring	= ~remove_items=>"ring46" add_items=>"mh#ringa(lring)"~
  END


  // ================
  // Sendai's Enclave
  // ================

  // Duelling a Bhaalspawn
  //
  // mh#belt7 - Girdle of Storm Giant Strength
  // mh#chan3 - Mail of Thalaera +5
  // mh#clck4 - Cloak of the Night Sky +3
  // mh#swrdb - Kanalruil +4

  MAKE_PATCH
    add_items=>"mh#chan3(armor) mh#swrdb(weapon1) mh#clck4(cloak) mh#belt7(belt)"
    add_proficiencies=>"bastardsword=>5"
  END

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "sengua03"
    edits	= "patch_data"
  END
END	// other_tob_locations


