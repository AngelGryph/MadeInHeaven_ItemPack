DEFINE_ACTION_FUNCTION item_pack_preproc
BEGIN
  // Construct the Tome spells

  ACTION_CLEAR_ARRAY xp_level_mage
  ACTION_CLEAR_ARRAY xp_level_cleric

  COPY_EXISTING - "xplevel.2da" "nowhere"
    COUNT_2DA_COLS cols

    SET max_level = cols - 1

    LPF 2daq_extract STR_VAR row = "MAGE" RET mage = rownum END
    LPF 2daq_extract STR_VAR row = "CLERIC" RET cleric = rownum END

    FOR (SET level = 1; level < max_level; ++level)
    BEGIN
      READ_2DA_ENTRY mage level cols current
      READ_2DA_ENTRY mage level+1 cols next
      SET $xp_level_mage("%level%") = (next - current)

      READ_2DA_ENTRY cleric level cols current
      READ_2DA_ENTRY cleric level+1 cols next
      SET $xp_level_cleric("%level%") = (next - current)
    END

  spl.copy[mh#mtome|source_location="spells"]
  [
    m.ab.clone{s_level=entry_index+2|number:i=%max_level%-2}
    m.ab_fx.alter{s_parameter1=$xp_level_mage("%p_level%")|match="s_opcode = 104"}
  ]

  spl.copy[mh#ctome|source_location="spells"]
  [
    m.ab.clone{s_level=entry_index+2|number:i=%max_level%-2}
    m.ab_fx.alter{s_parameter1=$xp_level_cleric("%p_level%")|match="s_opcode = 104"}
  ]


  // Make sure BG2's greater water elementals actually are
  // marked as water elementals for Staff- and Ring of Water.

  cre.edit[elemhydr elwatg01 ohbwat01|allow_missing:i=1]
  [
    m_class:=elemental_water
  ]
END	// item_pack_preproc


