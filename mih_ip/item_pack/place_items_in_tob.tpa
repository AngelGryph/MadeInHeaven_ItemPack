DEFINE_ACTION_FUNCTION place_items_in_tob
BEGIN
  // ==============
  // Watcher's Keep
  // ==============

  // Sister Garlena
  //
  // mh#blun3 - Battle Star +4
  // mh#dart3 - Zilzanzer's Magnificent Darts +1
  // mh#ioun1 - Sky Blue Ioun Stone
  // mh#ioun2 - Crystal Clear Ioun Stone
  // mh#potn3 - Philter of Purification
  // mh#swrd3 - Scimitar of the Seven +4
  // mh#shld6 - Mystery of the Dead +3
  // mh#wand2 - Elmira's Wand of Blasting
  // ring46   - Ring of Anti-Venom (from Vongoethe)

  MAKE_PATCH
    literal'1=>~ADD_STORE_ITEM "mh#blun3" AFTER "xbow15" #0 #0 #0 "identified" #1~
    literal'2=>~ADD_STORE_ITEM "mh#dart3" AFTER "dart02" #12 #0 #0 "identified" #5~
    literal'3=>~ADD_STORE_ITEM "mh#potn3" AFTER "potn52" #1 #0 #0 "identified" #2~
    literal'4=>~ADD_STORE_ITEM "mh#swrd3" AFTER "sw1h58" #0 #0 #0 "identified" #1~
    literal'5=>~ADD_STORE_ITEM "mh#shld6" LAST #0 #0 #0 "identified" #1~
    literal'6=>~ADD_STORE_ITEM "ring46" LAST #0 #0 #0 "identified" #1~
    literal'7=>~ADD_STORE_ITEM "mh#ioun1" LAST #0 #0 #0 "identified" #1~
    literal'8=>~ADD_STORE_ITEM "mh#ioun2" LAST #0 #0 #0 "identified" #1~
    literal'9=>~ADD_STORE_ITEM "mh#wand2" LAST #12 #0 #0 "identified" #1~
  END

  LAUNCH_ACTION_FUNCTION edit_store
    STR_VAR
    store	= "garlena"
    edits	= "patch_data"
  END

  // L3 - Portal Maze - Demon Wraith's lair
  //
  // mh#brac7 - Gloves of Evasion (on Demon Wraith's victims)
  // mh#belt7 - Girdle of Priestly Might (on Demon Wraith's victims)

  COPY_EXISTING "ar3014.are" "override"
    LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
      INT_VAR
      container_to_add_to	= 6
      STR_VAR
      item_to_add		= "mh#belt7"
    END
    LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
      INT_VAR
      container_to_add_to	= 2
      STR_VAR
      item_to_add		= "mh#brac7"
    END

  // L4 - Machine of Lum the Mad
  // 
  // mh#tome1 - Book of Exalted Deeds

  COPY_EXISTING "ar3017.are" "override"
    LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
      INT_VAR
      container_to_add_to	= 3
      charges1			= 1
      STR_VAR
      item_to_add		= "mh#tome1"
    END

  // L4 - Demilich's Lair
  //
  // mh#tome5 - Libram of Ineffable Damnation

  COPY_EXISTING "ar3027.are" "override"
    LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
      INT_VAR
      container_to_add_to	= 1
      charges1			= 1
      STR_VAR
      item_to_add		= "mh#tome5"
    END

  // L5 - Dragon's Lair

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "fsdragon"
    editstring	= ~add_items=>"mh#shld4(inv)"~
  END


  // =============
  // Ancient Grove
  // =============

  // Illesara's Ambush
  //
  // mh#brac4 - Bracers of the Wind

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "illasera"
    editstring	= ~add_items=>"mh#brac4(gloves)"~
  END


  // ========
  // Saradush
  // ========

  // Arcana Archives
  //
  // mh#amul7 - Dragon's Tear Amulet
  // mh#dart3 - Zilzanzer's Magnificent Darts +1
  // mh#tome3 - Libram of Silver Magic
  // mh#potn1 - Oil of Acid Resistance
  // mh#potn4 - Oil of Disenchantment
  // mh#ring6 - Ring of White Bone
  // mh#ring9 - Ring of Thex
  // mh#wand1 - Wand of Illusions

  MAKE_PATCH
    literal'1=>~ADD_STORE_ITEM "mh#dart3" AFTER "helm23" #12 #0 #0 "identified" #12~
    literal'2=>~ADD_STORE_ITEM "mh#tome3" AFTER "restore" #1 #0 #0 "identified" #1~
    literal'3=>~ADD_STORE_ITEM "mh#potn1" AFTER "potn45" #1 #0 #0 "identified" #9~
    literal'4=>~ADD_STORE_ITEM "mh#potn4" AFTER "potn55" #1 #0 #0 "identified" #5~
    literal'5=>~ADD_STORE_ITEM "mh#ring9" AFTER "ring02" #0 #0 #0 "identified" #1~
    literal'6=>~ADD_STORE_ITEM "mh#ring6" AFTER "ring02" #12 #0 #0 "identified" #1~
    literal'7=>~ADD_STORE_ITEM "mh#wand1" LAST #12 #12 #0 "identified" #1~
    literal'8=>~ADD_STORE_ITEM "mh#amul7" BEFORE "ring02" #2 #0 #0 "identified" #1~
  END

  LAUNCH_ACTION_FUNCTION edit_store
    STR_VAR
    store	= "25spell"
    edits	= "patch_data"
  END

  // Castle Saradush
  //
  // mh#brac6 - Silver Bracers of Kedl (on Karun the Black)
  // mh#staf4 - Staff of the Legion +5 (on Karun the Black)

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "gromg08"
    editstring	= ~remove_items=>"staf18" add_items=>"mh#brac6(gloves) mh#staf4(weapon1)"~
  END


  // ==================================
  // Forest of Mir / Marching Mountains
  // ==================================

  // Abandoned Temple of Bhaal
  //
  // mh#tome2 - Book of Vile Darkness
  // mh#robe5 - Nyalee's Robe of Stone (on Nyalee, unsurprisingly)

  COPY_EXISTING "ar5202.are" "override"
    LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
      INT_VAR
      container_to_add_to	= 2
      charges1			= 1
      STR_VAR
      item_to_add		= "mh#tome2"
    END

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "hgnya01"
    editstring	= ~ac=>10 add_items=>"mh#robe5(armor)" remove_items=>"brac15"~
  END

  // Fire Giant Temple
  //
  // mh#tome4 - Libram of Gainful Conjuration

  COPY_EXISTING "ar5204.are" "override"
    // Libram of Gainful Conjuration
    LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
      INT_VAR
      container_to_add_to	= 2
      charges1			= 1
      STR_VAR
      item_to_add		= "mh#tome4"
    END


  // =========
  // Amkethran
  // =========

  // Temple of Waukeen
  //
  // mh#potn3 - Philter of Purification
  // mh#shld7 - Waukeen's Defender +2

  MAKE_PATCH
    literal'1=>~ADD_STORE_ITEM "mh#potn3" AFTER "potn20" #1 #0 #0 "identified" #6~
    literal'2=>~ADD_STORE_ITEM "mh#shld7" LAST #0 #0 #0 "identified" #1~
  END

  LAUNCH_ACTION_FUNCTION edit_store
    STR_VAR
    store	= "amcler02"
    edits	= "patch_data"
  END

  // Sealed Cave
  //
  // mh#ring1 - Ring of the Lich +3 (on Vongoethe, replaces Ring of Anti-Venom)

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "amlich01"
    editstring	= ~remove_items=>"ring46" add_items=>"mh#ring1(lring)"~
  END


  // ================
  // Sendai's Enclave
  // ================

  // Duelling a Bhaalspawn
  //
  // mh#clck4 - Cloak of the Night Sky +3 (on Captain Egeissag)

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "sengua03"
    editstring	= ~add_items=>"mh#clck4(cloak)"~
  END
END	// place_items_in_tob


