DEFINE_ACTION_FUNCTION item_pack_resources
BEGIN
  // Set up graphical stuff

  LAUNCH_ACTION_FUNCTION install
    STR_VAR
    files	= "all"
    location	= "graphics"
    postfix	= "bam"
  END


  ACTION_BASH_FOR "override" "mh#vsio.\.bam"
  BEGIN
    LAUNCH_ACTION_FUNCTION install_vvc
      STR_VAR
      vvc		= "sporbit=>%BASH_FOR_RES%"
      location	 	= "graphics"
      editstring	= "anim=>%BASH_FOR_RES%"
    END
  END


  // Procedurally generate some effects

  OUTER_SET general_undead = IDS_OF_SYMBOL("general" "undead")
  OUTER_SET race_kobold = IDS_OF_SYMBOL("race" "kobold")
  OUTER_SET race_lycanthrope = IDS_OF_SYMBOL("race" "lycanthrope")
  OUTER_SET race_demonic = IDS_OF_SYMBOL("race" "demonic")
  OUTER_SET race_spider = IDS_OF_SYMBOL("race" "spider")
  OUTER_SET race_slime = IDS_OF_SYMBOL("race" "slime")
  OUTER_SET race_elemental = IDS_OF_SYMBOL("race" "elemental")
  OUTER_SET race_golem = IDS_OF_SYMBOL("race" "golem")
  OUTER_SET align_mask_evil = IDS_OF_SYMBOL("align" "mask_evil")

  OUTER_FOR (SET i = 1; i <= 3; ++i)
  BEGIN
    // To-hit vs. undead
    LAUNCH_ACTION_FUNCTION make_effect
      STR_VAR
      effect		= "mh#undh%i%"
      editstring	= "opcode=>178 timing=>1 parameter1=>%general_undead% parameter2=>3 parameter3=>%i%"
    END

    // To-hit vs. kobold
    LAUNCH_ACTION_FUNCTION make_effect
      STR_VAR
      effect		= "mh#kbdh%i%"
      editstring	= "opcode=>178 timing=>1 parameter1=>%race_kobold% parameter2=>4 parameter3=>%i%"
    END

    // To-hit vs. lycanthrope
    LAUNCH_ACTION_FUNCTION make_effect
      STR_VAR
      effect		= "mh#lych%i%"
      editstring	= "opcode=>178 timing=>1 parameter1=>%race_lycanthrope% parameter2=>4 parameter3=>%i%"
    END

    // To-hit vs. demonic
    LAUNCH_ACTION_FUNCTION make_effect
      STR_VAR
      effect		= "mh#demh%i%"
      editstring	= "opcode=>178 timing=>1 parameter1=>%race_demonic% parameter2=>4 parameter3=>%i%"
    END

    // To-hit vs. spider
    LAUNCH_ACTION_FUNCTION make_effect
      STR_VAR
      effect		= "mh#spdh%i%"
      editstring	= "opcode=>178 timing=>1 parameter1=>%race_spider% parameter2=>4 parameter3=>%i%"
    END

    // To-hit vs. slime
    LAUNCH_ACTION_FUNCTION make_effect
      STR_VAR
      effect		= "mh#slmh%i%"
      editstring	= "opcode=>178 timing=>1 parameter1=>%race_slime% parameter2=>4 parameter3=>%i%"
    END

    // To-hit vs. elemental
    LAUNCH_ACTION_FUNCTION make_effect
      STR_VAR
      effect		= "mh#elmh%i%"
      editstring	= "opcode=>178 timing=>1 parameter1=>%race_elemental% parameter2=>4 parameter3=>%i%"
    END

    // To-hit vs. golem
    LAUNCH_ACTION_FUNCTION make_effect
      STR_VAR
      effect		= "mh#golh%i%"
      editstring	= "opcode=>178 timing=>1 parameter1=>%race_golem% parameter2=>4 parameter3=>%i%"
    END

    // To-hit vs. evil
    LAUNCH_ACTION_FUNCTION make_effect
      STR_VAR
      effect		= "mh#evlh%i%"
      editstring	= "opcode=>178 timing=>1 parameter1=>%align_mask_evil% parameter2=>8 parameter3=>%i%"
    END

    // Damage vs. undead
    LAUNCH_ACTION_FUNCTION make_effect
      STR_VAR
      effect		= "mh#undd%i%"
      editstring	= "opcode=>179 timing=>1 parameter1=>%general_undead% parameter2=>3 parameter3=>%i%"
    END

    // Damage vs. golem
    LAUNCH_ACTION_FUNCTION make_effect
      STR_VAR
      effect		= "mh#gold%i%"
      editstring	= "opcode=>179 timing=>1 parameter1=>%race_golem% parameter2=>4 parameter3=>%i%"
    END

    // Damage vs. evil
    LAUNCH_ACTION_FUNCTION make_effect
      STR_VAR
      effect		= "mh#evld%i%"
      editstring	= "opcode=>179 timing=>1 parameter1=>%align_mask_evil% parameter2=>8 parameter3=>%i%"
    END

    // Extra slashing damage
    LAUNCH_ACTION_FUNCTION make_effect
      STR_VAR
      effect		= "mh#slsh%i%"
      editstring	= "opcode=>12 target=>2 timing=>1 parameter1=>%i% parameter2=>0x01000000"
    END

    // Extra piercing damage
    LAUNCH_ACTION_FUNCTION make_effect
      STR_VAR
      effect		= "mh#prce%i%"
      editstring	= "opcode=>12 target=>2 timing=>1 parameter1=>%i% parameter2=>0x00100000"
    END

    // Extra crushing damage
    LAUNCH_ACTION_FUNCTION make_effect
      STR_VAR
      effect		= "mh#crsh%i%"
      editstring	= "opcode=>12 target=>2 timing=>1 parameter1=>%i% parameter2=>0x00000000"
    END
  END


  // Vorpal Hit
  LAUNCH_ACTION_FUNCTION make_effect
    STR_VAR
    effect		= "mh#vphit"
    editstring		= "opcode=>13 target=>2 timing=>1 parameter2=>8"
  END

  // Vorpal hit message
  LAUNCH_ACTION_FUNCTION make_effect
    STR_VAR
    effect		= "mh#vhmsg"
    editstring		= "opcode=>139 target=>2 timing=>1 parameter1=>64285"
  END


  // Construct XP-gaining pseudo-spells for the Tomes
  // (Leave as is for now.  Not sure if this can be done at all in SFO.)

  COPY_EXISTING - "xplevel.2da" "dummy"
    COUNT_2DA_COLS cols
    COUNT_2DA_ROWS cols rows
    SET max_level = cols - 2

    FOR (SET y = 0; y < rows; ++y)
    BEGIN
      READ_2DA_ENTRY y 0 cols class

      PATCH_IF "%class%" STRING_EQUAL_CASE "mage"
            OR "%class%" STRING_EQUAL_CASE "cleric"
      BEGIN
        READ_2DA_ENTRY y 9 cols last_level_xp
        FOR (SET x = 10; x < (cols - 1); ++x)
        BEGIN
          READ_2DA_ENTRY y x cols next_level_xp
  	SET xp_gain = (next_level_xp - last_level_xp) / 2
  	SET last_level_xp = next_level_xp
  	
  	PATCH_IF "%class%" STRING_EQUAL_CASE "mage"
  	BEGIN
  	  TEXT_SPRINT $mage_xp_gain("%x%") "%xp_gain%"
  	END
  	ELSE
  	BEGIN
  	  TEXT_SPRINT $cleric_xp_gain("%x%") "%xp_gain%"
  	END
        END
      END
    END


  COPY "%MOD_FOLDER%/%component_loc%/spells/mh#mtome.spl" "override"
    LAUNCH_PATCH_FUNCTION CD_EXTEND-O-MATIC
      INT_VAR
      step_size		= 1
      step_dur		= 0
      level_cap		= max_level
      min_lev_alt	= 10
    END

    PHP_EACH mage_xp_gain AS level => xp_gain
    BEGIN
      LAUNCH_PATCH_FUNCTION ALTER_EFFECT
        INT_VAR
        match_opcode	= 104	// Stat: Experience Points
        header		= level - 10
        parameter1	= xp_gain
      END
    END


  COPY "%MOD_FOLDER%/%component_loc%/spells/mh#ctome.spl" "override"
    LAUNCH_PATCH_FUNCTION CD_EXTEND-O-MATIC
      INT_VAR
      step_size		= 1
      step_dur		= 0
      level_cap		= max_level
      min_lev_alt	= 10
    END

    PHP_EACH cleric_xp_gain AS level => xp_gain
    BEGIN
      LAUNCH_PATCH_FUNCTION ALTER_EFFECT
        INT_VAR
        match_opcode	= 104	// Stat: Experience Points
        header		= level - 10
        parameter1	= xp_gain
      END
    END
END	// item_pack_resources


