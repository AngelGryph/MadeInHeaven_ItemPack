DEFINE_ACTION_FUNCTION backwards_compatibility
BEGIN
  // mh#amul1 - Amulet of the Planes
  //
  // Opcode #344 (Turn Undead Level) does not exist in classic.

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "mh#amul1"
    editstring	= ~delete_effect_global=>"opcode = 323"~
  END


  // mh#dagg3 - Golemblight +2
  // mh#flai1 - Slime Crusher +1
  // mh#mace1 - Radiant Star +1
  // mh#swrd2 - Smythe's Slicer +1
  // mh#swrda - Holy Sword +2
  // 
  // Opcode #344 (Enchantment vs. Creature Type) does not exist in classic.
  // Adjust generic enchantment level to compensate.

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "mh#dagg3 mh#flai1 mh#mace1 mh#swrd2 mh#swrda"
    editstring	= ~enchantment=>4 delete_effect_global=>"opcode = 344"~
  END


  // mh#flai2 - Battlestar +4
  //
  // Opcode #346 (Save vs. Spell School) does not exist in classic.

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "mh#flai2"
    editstring	= ~delete_effect_global=>"opcode = 346"~
  END


  // mh#swrd7 - Mirthal +1
  // 
  // Opcode #344 (Enchantment vs. Creature Type) does not exist in classic.
  // Adjust generic enchantment level to compensate.

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "mh#swrd7"
    editstring	= ~enchantment=>2 delete_effect_global=>"opcode = 344"~
  END


  // mh#swrd4 - Sword of the Planes
  //
  // Action TransformItem() is not available in classic, script uses
  // TakeItemReplace() instead.  Set lore to 0 to avoid the swapped item
  // from being unidentified all the time.

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item              = "mh#swrd4 mh#swd4a"
    editstring        = "lore=>0"
  END


  // mh#mace3 - Skull Mace +2
  //
  // Opcode #341 (Spell Effect: Change Critical Hit Effect) does not exist
  // in classic.  Replace with flat 5% chance for insta-death.

  MAKE_PATCH
    delete_effect_global=>"opcode = 341"
    add_effect_inline=>~opcode=>13 target=>2 timing=>1 parameter1=>1 parameter2=>4 probability1=>4 save_vs_death=>1 save_bonus=>"-2"~
  END

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "mh#mace3"
    edits	= "patch_data"
  END
END	// backwards_compatibility


