DEFINE_ACTION_FUNCTION mh_run_task
  STR_VAR
  task		= "???"
BEGIN
  ACTION_IF NOT FILE_EXISTS "override/%task%.mih"
  BEGIN
    PRINT ~Running task %task%...~

    COPY "%MOD_FOLDER%/dummy.mih" "override/%task%.mih"

    ACTION_IF FILE_EXISTS "%MOD_FOLDER%/tra/%LANGUAGE%/%task%.tra"
    BEGIN
      LOAD_TRA "%MOD_FOLDER%/tra/%LANGUAGE%/%task%.tra"
    END

    ACTION_IF FILE_EXISTS "%MOD_FOLDER%/tasks/%task%.tpa"
    BEGIN
      REINCLUDE "%MOD_FOLDER%/tasks/%task%.tpa"
    END
    ELSE
    BEGIN
      FAIL ~Command file "%task%.tpa" not found!~
    END
  END
  ELSE
  BEGIN
    PRINT ~Skipping task %task%...~
  END
END


DEFINE_ACTION_MACRO mh_process_ini_file
BEGIN
  ACTION_IF FILE_EXISTS "%MOD_FOLDER%/%MOD_FOLDER%.ini"
  BEGIN
    COPY - "%MOD_FOLDER%/%MOD_FOLDER%.ini" "override"
      READ_2DA_ENTRIES_NOW ini_array 2
      FOR (i = 0; i < ini_array; ++i) BEGIN
        READ_2DA_ENTRY_FORMER ini_array i 0 key
        READ_2DA_ENTRY_FORMER ini_array i 1 value
        TO_LOWER key
        SPRINT $ini_hash(~%key%~) ~%value%~
      END
  END
END


