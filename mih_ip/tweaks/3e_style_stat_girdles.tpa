DEFINE_ACTION_FUNCTION 3e_style_stat_girdles
BEGIN
  // For speed and better control, doing this (mostly) the old way.

  COPY_EXISTING_REGEXP "^.+\.itm$" "override"
    READ_SHORT 0x001c category
    PATCH_IF (category == 0x03 || category == 0x06 || category == 0x07 || category == 0x0a || category == 0x20 || category == 0x49)
    BEGIN
      GET_OFFSET_ARRAY fx_array ITM_V10_GEN_EFFECTS
      PHP_EACH fx_array AS fx_index => fx_offset
      BEGIN
        READ_SHORT (fx_offset + 0x0000) opcode
        READ_LONG (fx_offset + 0x0004) parameter1
        READ_LONG (fx_offset + 0x0008) parameter2
	PATCH_IF ((opcode == 6 || opcode == 10 || opcode == 15 || opcode == 19 || opcode == 44 || opcode == 49) && parameter1 > 17 && parameter2 == 1)
	BEGIN
	  SET new_stat = parameter1 - 16
	  WRITE_LONG (fx_offset + 0x0004) new_stat
	  WRITE_LONG (fx_offset + 0x0008) 0

	  LAUNCH_PATCH_FUNCTION ITM_substitute_description
	    STR_VAR
	    arguments	= "%parameter1%=>+%new_stat%"
	  END
	END
	PATCH_IF (opcode == 97 && parameter2 == 1)
	BEGIN
	  WRITE_LONG (fx_offset + 0x0004) 0
	  WRITE_LONG (fx_offset + 0x0008) 0
	END
      END
    END

    BUT_ONLY_IF_IT_CHANGES
END	// 3e_style_stat_girdles


