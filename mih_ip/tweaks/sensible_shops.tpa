DEFINE_ACTION_FUNCTION sensible_shops
BEGIN
  // Check/retrieve ini variables

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini			= "bg1_move_thieving_stuff"
    RET
    bg1_move_thieving_stuff	= value
  END

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini			= "bg1_add_obscure_items"
    RET
    bg1_add_obscure_items	= value
  END

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini			= "bg2_swap_hammers"
    RET
    bg2_swap_hammers		= value
  END


  // BG1 section

  ACTION_IF is_bg1
  BEGIN
    // Move thieving stuff to Black Lily

    ACTION_IF bg1_move_thieving_stuff
    BEGIN
      MAKE_PATCH
        litvar_after_these=>"[leat05 dagg01]"
        litvar_item_list=>"[leat08 misc75]"
	clone_item_inline=>~match=>"item_resource is_in after_these" item_resource=>"entry_index from item_list" item_num_in_stock=>1~
      END
  
      LAUNCH_ACTION_FUNCTION edit_store
        STR_VAR
        store		= "stoblack"
        edits		= "patch_data"
      END
    END


    // Thunderhammer Smithy all fixes

    ACTION_CLEAR_ARRAY patch_data

    ACTION_IF bg1_move_thieving_stuff
    BEGIN
      ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data
      BEGIN
        remove_items=>"leat08 misc75"
      END
    END

    ACTION_IF bg1_add_obscure_items
    BEGIN
      ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data
      BEGIN
        litvar_after_these=>"[hamm01 chan04 sper01]"
        litvar_item_list=>"[hamm02 chan05 sper02]"
	clone_item_inline=>~match=>"item_resource is_in after_these" item_resource=>"entry_index from item_list" item_num_in_stock=>1~
      END
    END

    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store		= "taerum"
      edits		= "patch_data"
    END


    // Feldepost's Inn all fixes

    ACTION_CLEAR_ARRAY patch_data

    ACTION_IF bg1_add_obscure_items
    BEGIN
      ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data
      BEGIN
	clone_item_inline=>~match=>"item_resource = bow02" item_resource=>"bow04" item_num_in_stock=>1~
      END
    END

    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store		= "inn3351"
      edits		= "patch_data"
    END
  END	// is_bg1


  // BG2 section

  ACTION_IF is_bg2
  BEGIN
    // Hammer Swap: Borok's Fist <=> Aishideena

    ACTION_IF bg2_swap_hammers
    BEGIN
      LAUNCH_ACTION_FUNCTION edit_store
        STR_VAR
        store		= "ribald"
        editstring	= ~swap_items=>"hamm05=>hamm03"~
      END

      LAUNCH_ACTION_FUNCTION edit_area
        STR_VAR
        area		= "ar0204"
        editstring	= ~swap_items=>"hamm03=>hamm05"~
      END

      ACTION_IF enhanced_edition
      BEGIN
<<<<<<<< .../stratagems-inline/hammer_swap.baf
IF
  Global("OH_IMPORT02","GLOBAL",2)
  Global("OH_IMPORT02","AR0702",0)
THEN
  RESPONSE #100
    RemoveStoreItem("ribald","hamm03",1)
    SetGlobal("OH_IMPORT02","GLOBAL",100)
    SetGlobal("OH_IMPORT02","AR0702",1)
END
>>>>>>>>

        LAUNCH_ACTION_FUNCTION extend_area_script
          STR_VAR
	  area		= "ar0702"
	  top		= "hammer_swap"
	  inline	= "yes"
        END
      END
    END
  END	// is_bg2
END	// sensible_shops


