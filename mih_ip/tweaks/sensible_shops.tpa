DEFINE_ACTION_FUNCTION sensible_shops
BEGIN
  // BG1 section

  ACTION_IF is_bg1
  BEGIN
    // Move thieving stuff to Black Lily

    MAKE_PATCH
      litvar_after_these=>"[leat05 dagg01]"
      litvar_item_list=>"[leat08 misc75]"
      clone_item_inline=>~match=>"item_resource is_in after_these" item_resource=>"entry_index from item_list" item_num_in_stock=>1~
    END
  
    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store		= "stoblack"
      edits		= "patch_data"
    END


    MAKE_PATCH
      remove_items=>"leat08 misc75"
    END

    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store		= "taerum"
      edits		= "patch_data"
    END
  END	// is_bg1


  // BG2 section

  ACTION_IF is_bg2
  BEGIN
    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store		= "ribald"
      editstring	= ~swap_items=>"hamm05=>hamm03"~
    END

    LAUNCH_ACTION_FUNCTION edit_area
      STR_VAR
      area		= "ar0204"
      editstring	= ~swap_items=>"hamm03=>hamm05"~
    END

    ACTION_IF enhanced_edition
    BEGIN
<<<<<<<< .../stratagems-inline/hammer_swap.baf
IF
  Global("OH_IMPORT02","GLOBAL",2)
  Global("OH_IMPORT02","AR0702",0)
THEN
  RESPONSE #100
    RemoveStoreItem("ribald","hamm03",1)
    SetGlobal("OH_IMPORT02","GLOBAL",100)
    SetGlobal("OH_IMPORT02","AR0702",1)
END
>>>>>>>>

      LAUNCH_ACTION_FUNCTION extend_area_script
        STR_VAR
        area	= "ar0702"
	top	= "hammer_swap"
	inline	= "yes"
      END
    END
  END	// is_bg2
END	// sensible_shops


