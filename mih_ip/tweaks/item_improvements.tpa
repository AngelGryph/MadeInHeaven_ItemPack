DEFINE_ACTION_FUNCTION item_improvements
BEGIN
  // ==================================
  // Items that are both in BG1 and BG2
  // ==================================

  // brac07 - Gloves of Dexterity
  //
  // - Functions closer to its description in the DMG:
  //   +4 dex, +10 to pick pockets and open locks

  MAKE_PATCH
    say_description=>6
    patch_effect_global_inline=>~match=>"opcode = 15" parameter1=>4 parameter2=>0~
    add_effect_global_inline=>~number_to_add=>2 opcode=>"entry_index from [90 92]" target=>1 timing=>2 parameter1=>10~
  END

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "brac07"
    edits	= "patch_data"
  END


  // clck04 - Cloak of the Wolf
  //
  // - The wolf transformed into is a winter wolf.
  // - The cloak gives cold resistance.

  MAKE_PATCH
    add_effect_global_inline'1=>~opcode=>28 target=>1 timing=>2 parameter1=>100 parameter2=>1~
    add_effect_global_inline'2=>~opcode=>142 target=>1 timing=>2 parameter2=>25~
    patch_effect_global_inline=>~match=>opcode=53 parameter1=>0x7b03~
    add_effect_inline=>~opcode=>12 target=>2 timing=>1 parameter2=>0x00020000 dicenum=>2 dicesize=>4~
  END

  LAUNCH_ACTION_FUNCTION clone_item
    STR_VAR
    item		= "cdwolfm=>mh#wlfck"
    edits		= "patch_data"
  END


  MAKE_PATCH
    say_description=>7
    add_effect_global_inline'1=>~opcode=>28 target=>1 timing=>2 parameter1=>25~
    add_effect_global_inline'2=>~opcode=>142 target=>1 timing=>2 parameter2=>25~
    add_effect_global_inline'3=>~opcode=>63 target=>1 timing=>2~
    patch_effect_inline=>~match=>opcode=111 resource=>mh#wlfck~
  END

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "clck04"
    edits	= "patch_data"
  END


  // clck05 - Cloak of Balduran
  //
  // - Nerf the overpowered magic resistance
  // - Make it available in chapter 6 of BG2

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "baldurans_cloak_magic_resistance"
    RET
    baldurans_cloak_magic_resistance	= value
  END

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "baldurans_cloak_in_bg2"
    RET
    baldurans_cloak_in_bg2	= value
  END
  MAKE_PATCH
    patch_effect_global_inline=>~match=>opcode=166 parameter1=>%baldurans_cloak_magic_resistance%~
    substitute_description=>~25\%=>%baldurans_cloak_magic_resistance%\%~
  END

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "clck05"
    edits	= "patch_data"
  END


  ACTION_IF is_bg2 AND baldurans_cloak_in_bg2
  BEGIN
    MAKE_PATCH
      literal=>~ADD_STORE_ITEM "clck05" AFTER "chan11" #0 #0 #0 "identified" #1~
    END

    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store	= "ribald3"
      edits	= "patch_data"
    END
  END


  // clck20 - Cloak of the Shield
  //
  // - Effects are permanent while worn (+1 base AC, +4 vs. missiles)

  MAKE_PATCH
    say_description=>8
    add_effect_global_inline'1=>~opcode=>0 target=>1 timing=>2 parameter1=>1 parameter2=>0~
    add_effect_global_inline'2=>~opcode=>0 target=>1 timing=>2 parameter1=>4 parameter2=>2~
    delete_ability=>~~
  END

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "clck20"
    edits	= "patch_data"
  END



  // potn11 - Potion of Invulnerability
  //
  // - Grants +2 AC, +2 saves and immunity to normal weapons, as in P&P

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "potn11"
    editstring	= "say_description=>9"
  END

  LAUNCH_ACTION_FUNCTION externalise_item_ability
    STR_VAR
    item	= "potn11"
  END

  MAKE_PATCH
    patch_effect_inline'1=>~duration_if_variable=>60~
    patch_effect_inline'2=>~match=>"opcode is_in [0 33 34 35 36 37]" parameter1=>2 parameter2=>0~
    add_effect_inline'1=>~opcode=>120 duration=>60 target=>2 parameter2=>2 resist_dispel=>3~
    add_effect_inline'2=>~opcode=>142 duration=>60 target=>2 parameter2=>70 resist_dispel=>3~
    add_effect_inline'3=>~opcode=>206 duration=>60 target=>2 parameter1=>0 parameter2=>0 resist_dispel=>3 resource=>potn11~
  END

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "potn11"
    edits	= "patch_data"
  END


  // potn17 - Elixir of Health
  //
  // - Also cures blindness & deafness

  MAKE_PATCH
    say_description=>10
    add_effect_inline'1=>~opcode=>75 timing=>1 target=>2 at_end=>1~
    add_effect_inline'2=>~opcode=>240 timing=>1 target=>2 parameter2=>8 at_end=>1~
    add_effect_inline'3=>~opcode=>81 timing=>1 target=>2 at_end=>1~
    add_effect_inline'4=>~opcode=>240 timing=>1 target=>2 parameter2=>112 at_end=>1~
  END

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "potn17"
    edits	= "patch_data"
  END


  // shld20 - Kiel's Buckler
  //
  // Turn it into a +2 buckler.  Also correct icons on BG2.

  MAKE_PATCH
    say_description=>11
    price=>7000
    enchantment=>2
    patch_effect_inline=>~match=>"opcode = 0" parameter1=>"if parameter2 = 0 then 3 else -3"~
    icon=>"ishld20"
    icon_ground=>"gshld08"
    icon_carried=>"cshld20"
  END

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "shld20"
    edits	= "patch_data"
  END


  // ==============================
  // Items that are in BG2/SoA only
  // ==============================

  ACTION_IF is_bg2
  BEGIN
    // helm17 - Skull of Death
    //
    // This item will be made as described in Encyclopedia Arcana:
    // - Regenerates 1 HP/turn
    // - Casts Animate Dead, Finger of Death and Control undead 1/day each

    MAKE_PATCH
      say_description=>12
      add_effect_global_inline'1=>~opcode=>98 target=>1 timing=>2 parameter1=>60 parameter2=>3~
      add_effect_global_inline'2=>~opcode=>142 target=>1 timing=>2 parameter2=>87~
      clone_ability_inline=>~number_to_add=>2 at_end=>1~
      patch_ability_inline=>~ability_icon=>"entry_index from [spwi501b spwi713b spwi720b]" ability_target=>"entry_index from [4 1 1]" ability_range=>"entry_index from [20 40 40]"~
      patch_effect_inline=>~opcode=>"parent_index from [148 146 146]" target=>"parent_index from [1 2 2]" parameter1=>15 resource=>"parent_index from [spwi501b spwi713b spwi720b]"~
    END

    LAUNCH_ACTION_FUNCTION edit_item
      STR_VAR
      item	= "helm17"
      edits	= "patch_data"
    END


    // leat19 - Shadow Dragon Scale
    //
    // - Change acid resistance to negative plane protection.
    //   (This fits it better thematically.)

    MAKE_PATCH
      say_description=>13
      patch_effect_global_inline'1=>~match=>"opcode = 27" opcode=>101 parameter1=>0 parameter2=>216~
      patch_effect_global_inline'2=>~match=>"opcode = 142" parameter2=>90~
    END

    LAUNCH_ACTION_FUNCTION edit_item
      STR_VAR
      item	= "leat19"
      edits	= "patch_data"
    END
  END


  // ==============================
  // Items that are in BG2/ToB only
  // ==============================

  ACTION_IF has_tob
  BEGIN
    // helm31 - Helm of the Rock
    // helm32
    //
    // - This item will be usable by paladins and rangers.

    MAKE_PATCH
      unusable_paladin=>0
      unusable_ranger=>0
    END

    LAUNCH_ACTION_FUNCTION edit_item
      STR_VAR
      item	= "helm31 helm32"
      edits	= "patch_data"
    END


    // brac24 - Bard's Gloves
    // brac25 - Wondrous Gloves
    //
    // - This item will be usable by thief/mages and fighter/thief/mages.

    MAKE_PATCH
      unusable_mage_thief=>0
      unusable_fighter_mage_thief=>0
    END

    LAUNCH_ACTION_FUNCTION edit_item
      STR_VAR
      item	= "brac24 brac25"
      edits	= "patch_data"
    END


    // clck25 - Cloak of Stars
    //
    // Add +1 to saving throws as per P&P.

    MAKE_PATCH
      say_description=>14
      add_effect_global_inline=>~number_to_add=>5 opcode=>"entry_index from [33 34 35 36 37]" target=>1 timing=>2 parameter1=>1~
    END

    LAUNCH_ACTION_FUNCTION edit_item
      STR_VAR
      item	= "clck25"
      edits	= "patch_data"
    END
  END
END	// item_improvements


