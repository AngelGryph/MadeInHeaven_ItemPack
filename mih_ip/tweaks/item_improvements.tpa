DEFINE_ACTION_FUNCTION item_improvements
BEGIN
  // brac07 - Gloves of Dexterity
  //
  // - Functions closer to its description in the DMG:
  //   +4 dex, +10 to pick pockets and open locks

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "improve_gloves_of_dexterity"
    RET
    improve_gloves_of_dexterity	= value
  END

  ACTION_IF improve_gloves_of_dexterity
  BEGIN
    itm.edit[brac07]
    [
      m_identified_description := @6
      m.fx.alter{s_parameter1 = 4;;s_parameter2 = 0|match="s_opcode = 15"}
      m.fx.add{s_opcode = 90;;s_target = 1;;s_timing = 2;;s_parameter1 = 10}
      m.fx.add{s_opcode = 92;;s_target = 1;;s_timing = 2;;s_parameter1 = 10}
    ]
  END


  // clck04 - Cloak of the Wolf
  //
  // - The wolf transformed into is a winter wolf.
  // - The cloak gives cold resistance.

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "improve_cloak_of_the_wolf"
    RET
    improve_cloak_of_the_wolf	= value
  END

  ACTION_IF improve_cloak_of_the_wolf
  BEGIN
    itm.copy[cdwolfm=>mh#wlfck]
    [
      m.fx.add{s_opcode = 28;;s_target = 1;;s_timing = 2;;s_parameter1 = 100;;s_parameter2 = 1}
      //m.fx.add{s_opcode = 142;;s_target = 1;;s_timing = 2;;s_parameter2 = 25}
      m.fx.add{s_opcode = 63;;s_target = 1;;s_timing = 2}
      m.fx.alter{s_parameter1 = 0x7b03|match="s_opcode = 53"}
      m.ab_fx.add{s_opcode = 12;;s_target = 2;;s_timing = 1;;s_parameter2 = 0x00020000;;s_dicenumber = 2;;s_dicesize = 4}
    ]

    itm.edit[clck04]
    [
      m_identified_description := @7
      m.fx.add{s_opcode = 28;;s_target = 1;;s_timing = 2;;s_parameter1 = 25}
      m.fx.add{s_opcode = 142;;s_target = 1;;s_timing = 2;;s_parameter2 = 25}
      m.fx.add{s_opcode = 63;;s_target = 1;;s_timing = 2}
      m.fx.alter{s_resource:="mh#wlfck"|match="s_opcode = 111"}
    ]
  END


  // clck05 - Balduran's Cloak
  //
  // - Nerf the overpowered magic resistance

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "improve_baldurans_cloak"
    RET
    improve_baldurans_cloak	= value
  END

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "baldurans_cloak_magic_resistance"
    RET
    baldurans_cloak_magic_resistance	= value
  END

  ACTION_IF improve_baldurans_cloak
  BEGIN
    itm.edit[clck05]
    [
      m.fx.alter{s_parameter1 = %baldurans_cloak_magic_resistance%|match="s_opcode = 166"}
      //substitute_description=>~25\%=>%baldurans_cloak_magic_resistance%\%~
    ]
  END


  // clck20 - Shield Cloak
  //
  // - Effects are permanent while worn (+1 base AC, +4 vs. missiles)

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "improve_shield_cloak"
    RET
    improve_shield_cloak	= value
  END

  ACTION_IF improve_shield_cloak
  BEGIN
    itm.edit[clck20]
    [
      m_identified_description := @8
      m.fx.add{s_opcode = 0;;s_target = 1;;s_timing = 2;;s_parameter1 = 1;;s_parameter2 = 0}
      m.fx.add{s_opcode = 0;;s_target = 1;;s_timing = 2;;s_parameter1 = 4;;s_parameter2 = 0}
      m.ab.delete{}
    ]
  END


  // potn11 - Potion of Invulnerability
  //
  // - Grants +2 AC, +2 saves and immunity to normal weapons, as in P&P

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "improve_potion_of_invulnerability"
    RET
    improve_potion_of_invulnerability	= value
  END

  ACTION_IF improve_potion_of_invulnerability
  BEGIN
    ACTION_IF NOT FILE_EXISTS_IN_GAME "potn11.spl"
    BEGIN
      LAUNCH_ACTION_FUNCTION copy_item_to_spell
        STR_VAR
        item	= "potn11"
	spell	= "potn11"
      END
    END

    itm.edit[potn11]
    [
      m_identified_description := @9
      m.ab_fx.delete{}
      m.ab_fx.add{s_opcode = 146;;s_target := "Self";;s_timing := "InstantPermanent";;s_parameter2 = 1;;s_resource := "potn11"}
    ]

    spl.edit[potn11]
    [
      m.ab_fx.alter{s_duration = (s_duration > 5) ? 60 : s_duration}
      m.ab_fx.alter{s_parameter1 = 2;;s_parameter2 = 0|match=~INDEX(EXACT_MATCH "%s_opcode%" "0 33 34 35 36 37") != "-1"~}
      m.ab_fx.add{s_opcode = 120;;s_duration = 60;;s_target = 2;;s_parameter2 = 2;;s_dispel_resist = 3}
      m.ab_fx.add{s_opcode = 142;;s_duration = 60;;s_target = 2;;s_parameter2 = 70;;s_dispel_resist = 3}
      m.ab_fx.add{s_opcode = 206;;s_duration = 60;;s_target = 2;;s_dispel_resist = 3;;s_resource := "potn11"}
    ]
  END


  // shld20 - Kiel's Buckler
  //
  // Turn it into a +2 buckler.  Also correct icons on BG2.

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "improve_kiels_buckler"
    RET
    improve_kiels_buckler	= value
  END

  ACTION_IF improve_kiels_buckler
  BEGIN
    itm.edit[shld20]
    [
      m_identified_description := @11
      m_price = 7000
      m_enchantment = 2
      m.fx.alter{s_parameter1 = (s_parameter2 = 0) ? 3 : "-3"|match="s_opcode = 0"}
      m_icon := "ishld20"
      m_ground_icon := "gshld08"
      m_desc_image := "cshld20"
    ]
  END


  // helm17 - Skull of Death
  //
  // This item will be made as described in Encyclopedia Arcana:
  // - Regenerates 1 HP/turn
  // - Casts Animate Dead, Finger of Death and Control undead 1/day each

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "improve_skull_of_death"
    RET
    improve_skull_of_death	= value
  END

  ACTION_IF is_bg2 AND improve_skull_of_death
  BEGIN
    itm.edit[helm17]
    [
      m_identified_description := @12
      m.fx.add{s_opcode = 98;;s_target = 1;;s_timing = 2;;s_parameter1 = 60;;s_parameter2 = 3}
      m.fx.add{s_opcode = 142;;s_target = 1;;s_timing = 2;;s_parameter2 = 87}
      m.ab.delete{}
      m.ab.add{s_type = 3;;s_location = 3;;s_number_charges = 1;;s_when_drained = 3;;s_usable_after_id = 1;;s_recharge_after_resting = 1;;s_projectile = 1|number:i=3}
      m.ab.alter{s_icon := "spwi501b";;s_target = 4;;s_range = 20|match="entry_index = 0"}
      m.ab.alter{s_icon := "spwi713b";;s_target = 1;;s_range = 40|match="entry_index = 1"}
      m.ab.alter{s_icon := "spwi720b";;s_target = 1;;s_range = 40|match="entry_index = 2"}
      m.ab_fx.add{s_opcode = 148;;s_target = 1;;s_timing = 1;;s_parameter1 = 15;;s_resource := spwi501|match_parent="parent_index = 0"}
      m.ab_fx.add{s_opcode = 146;;s_target = 2;;s_timing = 1;;s_parameter1 = 15;;s_resource := spwi713|match_parent="parent_index = 1"}
      m.ab_fx.add{s_opcode = 146;;s_target = 2;;s_timing = 1;;s_parameter1 = 15;;s_resource := spwi720|match_parent="parent_index = 2"}
    ]
  END


  // leat19 - Shadow Dragon Scale
  //
  // - Change acid resistance to negative plane protection.
  //   (This fits it better thematically.)

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "improve_shadow_dragon_scale"
    RET
    improve_shadow_dragon_scale	= value
  END

  ACTION_IF is_bg2 AND improve_shadow_dragon_scale
  BEGIN
    itm.edit[leat19]
    [
      m_identified_description := @13
      m.fx.alter{s_opcode = 101;;s_parameter1 = 0;;s_parameter2 = 216|match="s_opcode = 27"}
      m.fx.alter{s_parameter2 = 90|match="s_opcode = 142"}
    ]
  END


  // ring34 - Ring of Spell Turning
  //
  // - Casts level 7 Spell Turning instead of level 5 Minor Spell Turning

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "improve_ring_of_spell_turning"
    RET
    improve_ring_of_spell_turning	= value
  END

  ACTION_IF is_bg2 AND improve_ring_of_spell_turning
  BEGIN
    itm.edit[ring34]
    [
      m.ab.alter{icon := "spwi701b"}
      m.ab_fx.alter{s_resource := "spwi701"|match="s_opcode = 146"}
    ]
  END


  // helm31 - Helm of the Rock
  // helm32
  //
  // - This item will be usable by paladins and rangers.

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "improve_helm_of_the_rock"
    RET
    improve_helm_of_the_rock	= value
  END

  ACTION_IF is_bg2 AND improve_helm_of_the_rock
  BEGIN
    itm.edit[helm31 helm32]
    [
      m_unusable_paladin = 0
      m_unusable_ranger = 0
    ]
  END


  // brac24 - Bard's Gloves
  // brac25 - Wondrous Gloves
  //
  // - This item will be usable by thief/mages and fighter/thief/mages.

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "improve_bards_gloves"
    RET
    improve_bards_gloves	= value
  END

  ACTION_IF is_bg2 AND improve_bards_gloves
  BEGIN
    itm.edit[brac24 brac25]
    [
      m_unusable_mage_thief = 0
      m_unusable_fighter_mage_thief = 0
    ]
  END


  // wa2robe - Robe of Vecna
  //
  // - The robe is now cursed with bad luck.

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "improve_robe_of_vecna"
    RET
    improve_robe_of_vecna	= value
  END

  ACTION_IF is_bg2 AND improve_robe_of_vecna
  BEGIN
    itm.edit[wa2robe]
    [
      m_cursed = 1
      m.fx.alter{s_parameter1 = 1|match="s_opcode = 1 AND s_parameter2 = 16"}
      m.fx.clone{s_opcode = (entry_index = 0) ? 22 : entry_index + 32;;s_parameter1 = "-2";;s_parameter2 = 0|number:i=6;;match="s_opcode = 0 AND s_parameter2 = 16"}
    ]
  END
END	// item_improvements


