DEFINE_ACTION_FUNCTION extra_healing_in_bg1
BEGIN
  // Distribute potions of extra healing to major temples and some shops.

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "extra_healing_shops"
    RET
    extra_healing_shops	= value
  END

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "extra_healing_minimum"
    RET
    extra_healing_minimum	= value
  END

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "extra_healing_maximum"
    RET
    extra_healing_maximum	= value
  END

  ACTION_IF extra_healing_shops
  BEGIN
    sto.edit[tem0002 tem0131 tem0132 tem3402 tem4003 stoblack ulgoth erdane]
    [
      ADD_STORE_ITEM + "potn52" AFTER "potn08" #1 #0 #0 "identified" (RANDOM(%extra_healing_minimum% %extra_healing_maximum%))
    ]
  END


  // Replace large stacks of healing potions in Durlag's Tower with
  // equivalent stacks of potions of extra healing.

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "extra_healing_durlag"
    RET
    extra_healing_durlag	= value
  END

  ACTION_IF extra_healing_durlag
  BEGIN
    are.edit[%DurlagsTower_D1% %DurlagsTower_D2% %DurlagsTower_D3% %DurlagsTower_D4%]
    [
      m.container_item.alter{s_resref := "potn52";;s_charges_1 = (s_charges_1 + 1) / 3|match=~"%s_resref%" == "potn8" AND s_charges_1 > 2~}
    ]
  END
END	// extra_healing_in_bg1


