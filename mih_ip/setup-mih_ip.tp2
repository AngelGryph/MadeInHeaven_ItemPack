// Made in Heaven - Item Pack
//
// WeiDU installer

BACKUP "mih_ip/backup"

AUTHOR "angel@pearlgates.net"
VERSION v3

//MODDER missing_eval none missing_resref none
ASK_EVERY_COMPONENT
NO_IF_EVAL_BUG
AUTO_EVAL_STRINGS
AUTO_TRA "mih_ip/language/%s"

ALWAYS
  // We no longer support vanilla BG1 or Tutu, it's too much of a hassle.

  ACTION_IF GAME_IS "bg1 totsc tutu tutu_totsc"
  BEGIN
    FAIL ~Sorry, but this mod no longer supports this version of the game.~
  END


  // Load compatibility variables for various implementations of BG1.

  INCLUDE "%MOD_FOLDER%/lib/g3_bgt_cpmvars.tpa"
  INCLUDE "%MOD_FOLDER%/lib/g3_bgee_cpmvars.tpa"
  INCLUDE "%MOD_FOLDER%/lib/g3_eet_cpmvars.tpa"


  // BG(2)EE compat: handle charsets

  ACTION_DEFINE_ARRAY mh#reload BEGIN "setup-%MOD_FOLDER%" END
  ACTION_DEFINE_ARRAY mh#noconvert BEGIN END

  LAUNCH_ACTION_FUNCTION HANDLE_CHARSETS
    INT_VAR
    infer_charset	= 1
    STR_VAR
    default_language	= "english"
    tra_path		= "%MOD_FOLDER%/language"
    iconv_path	= "%MOD_FOLDER%/bin/win32/iconv"
    noconvert_array	= "mh#noconvert"
    reload_array	= "mh#reload"
  END


  // Load macro/function libraries

  INCLUDE "%MOD_FOLDER%/lib/mh_functions.tph"
  INCLUDE "%MOD_FOLDER%/lib/cd_functions.tph"


  // Read the .ini file for this mod

  LAUNCH_ACTION_MACRO mh_process_ini_file
END	// ALWAYS block


LANGUAGE ~English~
         ~english~
	 ~%MOD_FOLDER%/language/english/setup-%MOD_FOLDER%.tra~


// Item Pack

BEGIN ~MiH Item Pack~

LAUNCH_ACTION_FUNCTION mh_run_task
  STR_VAR
  task			= "install_items"
END

ACTION_IF GAME_INCLUDES "bg1" AND $ini_hash("place_items_in_bg1")
BEGIN
  LAUNCH_ACTION_FUNCTION mh_run_task
    STR_VAR
    task		= "place_items_in_bg1"
  END
END

ACTION_IF GAME_INCLUDES "sod" AND $ini_hash("place_items_in_sod")
BEGIN
  LAUNCH_ACTION_FUNCTION mh_run_task
    STR_VAR
    task		= "place_items_in_sod"
  END
END

ACTION_IF GAME_INCLUDES "bg2" AND $ini_hash("place_items_in_bg2")
BEGIN
  LAUNCH_ACTION_FUNCTION mh_run_task
    STR_VAR
    task		= "place_items_in_bg2"
  END
END

ACTION_IF GAME_INCLUDES "tob" AND $ini_hash("place_items_in_tob")
BEGIN
  LAUNCH_ACTION_FUNCTION mh_run_task
    STR_VAR
    task		= "place_items_in_tob"
  END
END


// Improved Cloak of the Wolf

BEGIN ~Improved Cloak of the Wolf~
GROUP ~Optional Item Tweaks~

LAUNCH_ACTION_FUNCTION mh_run_task
  STR_VAR
  task			= "improve_cloak_of_wolf"
END


// Nerfed Cloak of Balduran

BEGIN ~Nerfed Cloak of Balduran~
GROUP ~Optional Item Tweaks~

LAUNCH_ACTION_FUNCTION mh_run_task
  STR_VAR
  task			= "nerf_cloak_of_balduran"
END


// Alternative Healing Potions

BEGIN ~2d4+2, 3d8+3, 6d8+2 (P&P)~
SUBCOMPONENT ~Alternative Healing Potions~
GROUP ~Optional Item Tweaks~

LAUNCH_ACTION_FUNCTION mh_run_task
  STR_VAR
  task			= "make_healing_potions_pnp"
END


BEGIN ~9, 18, 27 (Icewind Dale)~
SUBCOMPONENT ~Alternative Healing Potions~
GROUP ~Optional Item Tweaks~

LAUNCH_ACTION_FUNCTION mh_run_task
  STR_VAR
  task			= "make_healing_potions_iwd"
END


BEGIN ~9, 27, 40 (Baldur's Gate 2)~
SUBCOMPONENT ~Alternative Healing Potions~
GROUP ~Optional Item Tweaks~

LAUNCH_ACTION_FUNCTION mh_run_task
  STR_VAR
  task			= "make_healing_potions_bg2"
END


// P&P Potion of Invulnerability

BEGIN ~P&P Potion of Invulnerability~
GROUP ~Optional Item Tweaks~

LAUNCH_ACTION_FUNCTION mh_run_task
  STR_VAR
  task			= "improve_potion_of_invulnerability"
END


// Elixirs of Health cure blindness/deafness.

BEGIN ~Elixir of Health cures blindness & deafness~
GROUP ~Optional Item Tweaks~

LAUNCH_ACTION_FUNCTION mh_run_task
  STR_VAR
  task			= "improve_elixir_of_health"
END


// Improved Skull of Death

BEGIN ~Improved Skull of Death~
GROUP ~Optional Item Tweaks~

REQUIRE_PREDICATE GAME_INCLUDES "bg2" ~This component is not applicable to your game.~

LAUNCH_ACTION_FUNCTION mh_run_task
  STR_VAR
  task			= "improve_skull_of_death"
END


// BG1-style wands

BEGIN ~BG1-style Wands of Frost & -Lightning~
GROUP ~Optional Item Tweaks~

REQUIRE_PREDICATE NOT GAME_IS "bgee" ~This component is not applicable to your game.~

LAUNCH_ACTION_FUNCTION mh_run_task
  STR_VAR
  task			= "tweak_wands_bg1"
END


