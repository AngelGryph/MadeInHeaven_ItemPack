DEFINE_ACTION_FUNCTION bg2_items_in_bg1
BEGIN
  // Missing BG2 items in BG1EE

  ACTION_IF GAME_IS "bgee"
  BEGIN
    LAUNCH_ACTION_FUNCTION install_item
      STR_VAR
      item		= "amul19"
      location		= "items"
      editstring	= "say_name=>0 say_description=>1"
    END

    LAUNCH_ACTION_FUNCTION install_item
      STR_VAR
      item		= "boot07"
      location		= "items"
      editstring	= "say_name=>2 say_description=>3"
    END

    LAUNCH_ACTION_FUNCTION install_item
      STR_VAR
      item		= "clck23"
      location		= "items"
      editstring	= "say_name=>4 say_description=>5"
    END

    LAUNCH_ACTION_FUNCTION install_item
      STR_VAR
      item		= "clck23"
      location		= "items"
      editstring	= "say_name=>6 say_description=>7"
    END

    LAUNCH_ACTION_FUNCTION install_item
      STR_VAR
      item		= "sw1h37"
      location		= "items"
      editstring	= "say_name=>8 say_description=>9 price=>4800"
    END
  END


  // High Hedge (Thalantyr)
  //
  // ring46 - Ring of Anti-Venom

  MAKE_PATCH
    literal'1=>~ADD_STORE_ITEM "ring46" AFTER "misc72" #0 #0 #0 "identified" #1~
  END

  LAUNCH_ACTION_FUNCTION edit_store
    STR_VAR
    store	= "highhedg"
    edits	= "patch_data"
  END


  // Sorcerous Sundries
  //
  // amul19 - Amulet of 5% Magic Resistance
  // amul23 - Amulet of Spell Warding

  MAKE_PATCH
    literal'1=>~ADD_STORE_ITEM "amul19" LAST #0 #0 #0 "identified" #1~
    literal'2=>~ADD_STORE_ITEM "amul23" LAST #0 #0 #0 "identified" #1~
  END

  LAUNCH_ACTION_FUNCTION edit_store
    STR_VAR
    store	= "sto0703"
    edits	= "patch_data"
  END


  // Black Lily
  //
  // ring35 - Ring of Lock Picks
  // ring36 - Ring of Danger Sense

  MAKE_PATCH
    literal'1=>~ADD_STORE_ITEM "ring35" LAST #0 #0 #0 "identified" #1~
    literal'2=>~ADD_STORE_ITEM "ring36" LAST #0 #0 #0 "identified" #1~
  END

  LAUNCH_ACTION_FUNCTION edit_store
    STR_VAR
    store	= "stoblack"
    edits	= "patch_data"
  END


  // Fisherman's Lake
  //
  // brac18 - Gloves of Missile Snaring

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "zekar"
    editstring	= ~add_items=>"brac18(gloves)"~
  END


  // Bandit Camp
  //
  // sw1h37 - Taragarth the Bloodbrand +1

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "hakt"
    editstring	= ~remove_items=>"sw1h01 comps01" add_items=>"sw1h37(weapon1)"~
  END


  // Cloakwood
  //
  // boot07 - Boots of Elvenkind
  // clck23 - Cloak of Elvenkind

  COPY_EXISTING "%CloakwoodWyverns_WyvernCave%.are" "override"
    PATCH_FOR_EACH item_to_add IN "boot07" "clck23"
    BEGIN
      LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
        INT_VAR
        container_to_add_to	= 1
        STR_VAR
        item_to_add
      END
    END


  // Durlag's Tower
  //
  // dagg11 - Boomerang Dagger +2

  COPY_EXISTING "%DurlagsTower_D4%.are" "override"
    // Boomerang Dagger +2
    LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
      INT_VAR
      container_to_add_to	= 11
      STR_VAR
      item_to_add		= "dagg11"
    END
END	// bg2_items_in_bg1


