DEFINE_ACTION_FUNCTION place_iwd_items_in_bg2
BEGIN
  // ===================
  // Waukeen's Promenade
  // ===================

  // Arnolius the Dwarf
  //
  // mh#iwd08 - Black Swan Armor +2

  MAKE_PATCH
    literal'1=>~ADD_STORE_ITEM "mh#iwd08" AFTER "plat10" #0 #0 #0 "identified" #1~
  END

  LAUNCH_ACTION_FUNCTION edit_store
    STR_VAR
    store	= "shop02"
    edits	= "patch_data"
  END


  // Adventurer's Market
  //
  // mh#iwd12 - Ogi-Luc's Great Robe

  MAKE_PATCH
    clone_item_inline=>~match=>"item_resource = chan11" item_resource=>"mh#iwd12" item_num_in_stock=>1~
  END

  LAUNCH_ACTION_FUNCTION edit_store
    STR_VAR
    store	= "ribald3"
    edits	= "patch_data"
  END


  // ==============
  // Docks District
  // ==============

  // Mad Cleric of Cyric

  MAKE_PATCH
    remove_items=>"staf01"
    add_items=>"mh#iwd23(weapon1) shld04(shield)"
    add_proficiencies=>"mace=>2 swordandshield=>2"
  END

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "dcleric"
    edits	= "patch_data"
  END


  // =============
  // Planar Sphere
  // =============

  // Stolen by Lavok
  //
  // mh#iwd10 - Shield Ring

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "lavok01 lavok02"
    editstring	= ~add_items=>"mh#iwd10(rring)"~
  END


  // ===============
  // Temple District
  // ===============

  // Temple of Lathander
  //
  // mh#iwd01 - Girdle of Beatification
  // mh#iwd06 - Blessed Helm of Lathander
  // mh#iwd13 - Shield of the Revenant +2

  MAKE_PATCH
    clone_item_inline'1=>~match=>"item_resource = staf05" item_resource=>"mh#iwd13" item_num_in_stock=>1~
    clone_item_inline'2=>~match=>"item_resource = helm06" item_resource=>"mh#iwd06" item_num_in_stock=>1 item_charges_1=>1 item_charges_2=>1 item_charges_3=>1~
    literal=>~ADD_STORE_ITEM "mh#iwd01" LAST #0 #0 #0 "identified" #1~
  END

  LAUNCH_ACTION_FUNCTION edit_store
    STR_VAR
    store	= "temlath"
    edits	= "patch_data"
  END


  // Undead City in Lower Reaches (Cult of the Eyeless)
  //
  // mh#iwd04 - Spinesheath +1

  MAKE_PATCH
    add_item_inline=>~match=>"parent_index = 2" item_resource=>"mh#iwd04"~
  END

  LAUNCH_ACTION_FUNCTION edit_area
    STR_VAR
    area	= "ar0206"
    edits	= "patch_data"
  END


  // =========
  // Trademeet
  // =========

  // Temple of Waukeen
  //
  // mh#iwd15 - Waukeen's Defender +2

  MAKE_PATCH
    litvar_items=>"[mh#iwd15]"
    add_item_inline=>~number_to_add=>"length items" at_end=>1 item_resource=>"entry_index from items" item_num_in_stock=>1~
  END

  LAUNCH_ACTION_FUNCTION edit_store
    STR_VAR
    store	= "temsup"
    edits	= "patch_data"
  END


  // ============
  // Temple Ruins
  // ============

  // Blessings of Amaunator
  //
  // mh#iwd19 - Amaunator's Legacy +3

  MAKE_PATCH
    add_item_inline=>~match=>"parent_index = 3" item_resource=>"mh#iwd19" item_charges_2=>1~
  END

  LAUNCH_ACTION_FUNCTION edit_area
    STR_VAR
    area	= "ar1401"
    edits	= "patch_data"
  END


  // =========
  // Underdark
  // =========

  // Ust Natha
  //
  // mh#iwd02 - Girdle of Ogre Blood

  MAKE_PATCH
    literal=>~ADD_STORE_ITEM "mh#iwd02" AFTER "shld22" #1 #0 #0 "identified" #1~
  END

  LAUNCH_ACTION_FUNCTION edit_store
    STR_VAR
    store	= "uddrow23"
    edits	= "patch_data"
  END


  // =============
  // Suldanessenar
  // =============

  // Nizidramanii'yt Hoard
  //
  // mh#iwd20 - Pale Justice +4

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "dragblac"
    editstring	= ~add_items=>"mh#iwd20(qitem)"~
  END
END	// place_iwd_items_in_bg2


