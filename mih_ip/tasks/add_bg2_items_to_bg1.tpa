// Missing BG2 items in BG1EE

ACTION_IF GAME_IS "bgee"
BEGIN
  COPY "%MOD_FOLDER%/items/bg2_items/amul19.itm" "override"
    SAY NAME2 @0
    SAY IDENTIFIED_DESC @1


  COPY "%MOD_FOLDER%/items/bg2_items/boot07.itm" "override"
    SAY NAME2 @2
    SAY IDENTIFIED_DESC @3


  COPY "%MOD_FOLDER%/items/bg2_items/clck23.itm" "override"
    SAY NAME2 @4
    SAY IDENTIFIED_DESC @5


  COPY "%MOD_FOLDER%/items/bg2_items/ring46.itm" "override"
    SAY NAME2 @6
    SAY IDENTIFIED_DESC @7


  COPY "%MOD_FOLDER%/items/bg2_items/sw1h37.itm" "override"
    SAY NAME2 @8
    SAY IDENTIFIED_DESC @9

    WRITE_LONG 0x0034 4800
END


// High Hedge (Thalantyr)

COPY_EXISTING "highhedg.sto" "override"
  // Ring of Anti-Venom
  ADD_STORE_ITEM "ring46" AFTER "misc72" #0 #0 #0 "identified" #1


// Sorcerous Sundries

COPY_EXISTING "sto0703.sto" "override"
  // Amulet of 5% Magic Resistance
  ADD_STORE_ITEM "amul19" LAST #0 #0 #0 "identified" #1

  // Amulet of Spell Warding
  ADD_STORE_ITEM "amul23" LAST #0 #0 #0 "identified" #1


// Black Lily

COPY_EXISTING "stoblack.sto" "override"
  // Ring of Lock Picks
  ADD_STORE_ITEM "ring35" LAST #0 #0 #0 "identified" #1

  // Ring of Danger Sense
  ADD_STORE_ITEM "ring36" LAST #0 #0 #0 "identified" #1


// Fisherman's Lake

COPY_EXISTING "zekar.cre" "override"
  // Gloves of Missile Snaring
  ADD_CRE_ITEM "brac18" #0 #0 #0 "unstealable" "gloves"


// Bandit Camp

COPY_EXISTING "hakt.cre" "override"
  // Taragarth the Bloodbrand +1
  ADD_CRE_ITEM "sw1h37" #0 #0 #0 "unstealable" "weapon1" EQUIP
  REMOVE_CRE_ITEM "comps01" "sw1h01"


// Cloakwood

COPY_EXISTING "%CloakwoodWyverns_WyvernCave%.are" "override"
  // Cloak & Boots of Elvenkind
  PATCH_FOR_EACH item_to_add IN "boot07" "clck23"
  BEGIN
    LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
      INT_VAR
      container_to_add_to	= 1
      STR_VAR
      item_to_add
    END
  END


// Durlag's Tower

COPY_EXISTING "%DurlagsTower_D4%.are" "override"
  // Boomerang Dagger +2
  LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
    INT_VAR
    container_to_add_to	= 11
    STR_VAR
    item_to_add		= "dagg11"
  END


