// Sensible Item Shops (BG1)


// Fix illogical overabundance of large shields in BG1 shops

ACTION_IF $ini_hash("bg1_fix_large_shields")
BEGIN
  COPY_EXISTING "friend.sto" "override"
                "sto1112.sto" "override"
                "sto1320.sto" "override"
                "sto2606.sto" "override"
                "sto4803.sto" "override"
	        "ulgoth.sto" "override"
    REMOVE_STORE_ITEM "shld05"

    LAUNCH_PATCH_FUNCTION REPLACE_STORE_ITEM
      INT_VAR
      number_in_stock	= 5
      STR_VAR
      old_item		= "shld06"
      new_item		= "shld05"
    END

    IF "shld06"	// Already fixed in some shops in EE
    IF_EXISTS	// Some of these are not in BGT for some reason
    BUT_ONLY_IF_IT_CHANGES
END


// Move thieving stuff to Black Lily

ACTION_IF $ini_hash("bg1_move_thieving_stuff")
BEGIN
  COPY_EXISTING "stoblack.sto" "override"
    ADD_STORE_ITEM "leat08" AFTER "leat05" #0 #0 #0 "identified" #1
    ADD_STORE_ITEM "misc75" AFTER "dagg01" #0 #0 #0 "identified" #1
END


// Thunderhammer Smithy all fixes

COPY_EXISTING "taerum.sto" "override"
  PATCH_IF $ini_hash("bg1_fix_large_shields")
  BEGIN
    ADD_STORE_ITEM "shld06" AFTER "shld05" #0 #0 #0 "identified" #1
  END

  PATCH_IF $ini_hash("bg1_move_thieving_stuff")
  BEGIN
    REMOVE_STORE_ITEM "leat08" "misc75"
  END

  PATCH_IF $ini_hash("bg1_add_obscure_items")
  BEGIN
    ADD_STORE_ITEM "hamm02" AFTER "hamm01" #0 #0 #0 "identified" #1
    ADD_STORE_ITEM "chan05" AFTER "chan04" #0 #0 #0 "identified" #1
    ADD_STORE_ITEM "sper02" AFTER "sper01" #0 #0 #0 "identified" #1
  END

  BUT_ONLY_IF_IT_CHANGES


// Feldepost's Inn all fixes

COPY_EXISTING "inn3351.sto" "override"
  PATCH_IF $ini_hash("bg1_fix_large_shields")
  BEGIN
    ADD_STORE_ITEM "shld06" AFTER "shld04" #0 #0 #0 "identified" #1
  END

  PATCH_IF $ini_hash("bg1_add_obscure_items")
  BEGIN
    ADD_STORE_ITEM "bow04" AFTER "bow02" #0 #0 #0 "identified" #1
  END

  BUT_ONLY_IF_IT_CHANGES


// Thalantyr the *Conjurer*


ACTION_IF $ini_hash("bg1_fix_thalantyr")
BEGIN
  COPY_EXISTING "highhedg.sto" "override"
    // Remove Divination school spell scrolls
    REMOVE_STORE_ITEM "scrl75 scrl76 scrl87"

    // Melf's Acid Arrow
    ADD_STORE_ITEM "scrl95" AFTER "scrl94" #1 #0 #0 "identified" #3

    // Glitterdust
    ADD_STORE_ITEM "scrla3" AFTER "scrla2 scrl1q" #1 #0 #0 "identified" #3

    // Power Word: Sleep (for BGT, EE already has it)
    ADD_STORE_ITEM "scrl6e" AFTER "scrla2 scrl1q" #1 #0 #0 "identified" #3

    // Monster Summoning I
    ADD_STORE_ITEM "scrl1l" AFTER "scrl1i" #1 #0 #0 "identified" #3

    // Ghost Armor
    ADD_STORE_ITEM "scrl1t" AFTER "scrl1q" #1 #0 #0 "identified" #3

    // Monster Summoning II
    ADD_STORE_ITEM "scrl2a" BEFORE "wand08" #1 #0 #0 "identified" #1

    // Spider Spawn
    ADD_STORE_ITEM "scrl6r" BEFORE "wand08" #1 #0 #0 "identified" #1
END


