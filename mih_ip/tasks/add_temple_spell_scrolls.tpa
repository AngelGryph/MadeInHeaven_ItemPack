// Add scrolls of Cure Light Wounds and Slow Poison

COPY_EXISTING "scrl56.itm" "override/mh#scclw.itm"
  WRITE_LONG 0x0034 100
  WRITE_ASCII 0x003a "sppr103a"

  LAUNCH_PATCH_FUNCTION ALTER_ITEM_HEADER
    STR_VAR
    icon		= "sppr103a"
  END

  LAUNCH_PATCH_FUNCTION ALTER_EFFECT
    INT_VAR
    match_opcode	= 146	// Spell: Cast Spell (at Creature)
    parameter1		= 5
    STR_VAR
    resource		= "sppr103"
  END

  INNER_PATCH_FILE "sppr103.spl"
  BEGIN
    READ_LONG NAME1 name
    READ_LONG UNIDENTIFIED_DESC desc
  END

  WRITE_LONG NAME2 name
  WRITE_LONG IDENTIFIED_DESC desc


COPY_EXISTING "scrl59.itm" "override/mh#scslp.itm"
  WRITE_LONG 0x0034 200
  WRITE_ASCII 0x003a "sppr212a"

  LAUNCH_PATCH_FUNCTION ALTER_ITEM_HEADER
    STR_VAR
    icon		= "sppr212a"
  END

  LAUNCH_PATCH_FUNCTION ALTER_EFFECT
    INT_VAR
    match_opcode	= 146	// Spell: Cast Spell (at Creature)
    parameter1		= 5
    STR_VAR
    resource		= "sppr212"
  END

  INNER_PATCH_FILE "sppr212.spl"
  BEGIN
    READ_LONG NAME1 name
    READ_LONG UNIDENTIFIED_DESC desc
  END

  WRITE_LONG NAME2 name
  WRITE_LONG IDENTIFIED_DESC desc


ACTION_IF GAME_INCLUDES "bg1"
BEGIN
  COPY_EXISTING "tem0002.sto" "override"
                "tem0131.sto" "override"
                "tem0132.sto" "override"
                "tem2304.sto" "override"
                "tem2601.sto" "override"
                "tem3402.sto" "override"
                "tem4003.sto" "override"
                "tem4802.sto" "override"
    ADD_STORE_ITEM "mh#scclw" BEFORE "scrlpet" #1 #0 #0 "identified" #5
    ADD_STORE_ITEM "mh#scslp" BEFORE "scrlpet" #1 #0 #0 "identified" #5
    ADD_STORE_ITEM "scrl56" BEFORE "scrlpet" #1 #0 #0 "identified" #2
    ADD_STORE_ITEM "scrl58" BEFORE "scrlpet" #1 #0 #0 "identified" #2
    ADD_STORE_ITEM "scrl59" BEFORE "scrlpet" #1 #0 #0 "identified" #2
    ADD_STORE_ITEM "restore" BEFORE "scrlpet" #1 #0 #0 "identified" #1
END


ACTION_IF GAME_INCLUDES "bg2"
BEGIN
  COPY_EXISTING "amcler02.sto" "override"
                "bhelm.sto" "override"
                "dawnmas.sto" "override"
                "doghma.sto" "override"
                "garlena.sto" "override"
                "govwau01.sto" "override"
                "helmpr.sto" "override"
                "kpchap01.sto" "override"
                "ppumb01.sto" "override"
                "sahpr1.sto" "override"
                "sartem01.sto" "override"
                "suelf10.sto" "override"
                "talmiss.sto" "override"
                "temhelm.sto" "override"
                "temlath.sto" "override"
                "temple.sto" "override"
                "temsup.sto" "override"
                "temtalos.sto" "override"
                "wilmat.sto" "override"
    ADD_STORE_ITEM "mh#scclw" BEFORE "scrlpet restore" #1 #0 #0 "identified" #10
    ADD_STORE_ITEM "mh#scslp" BEFORE "scrlpet restore" #1 #0 #0 "identified" #10
    ADD_STORE_ITEM "scrl56" BEFORE "scrlpet restore" #1 #0 #0 "identified" #5
    ADD_STORE_ITEM "scrl58" BEFORE "scrlpet restore" #1 #0 #0 "identified" #5
    ADD_STORE_ITEM "scrl59" BEFORE "scrlpet restore" #1 #0 #0 "identified" #5
    ADD_STORE_ITEM "scrl61" BEFORE "scrlpet restore" #1 #0 #0 "identified" #2
END


